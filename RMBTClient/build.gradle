apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.2.0'
    }
}

dependencies {
    implementation group: 'org.apache.httpcomponents', name: 'httpcore', version: '4.4.5'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.2'
    implementation group: 'net.sf.jopt-simple', name: 'jopt-simple', version: '5.0.4'
    implementation group: 'dnsjava', name: 'dnsjava', version: '2.1.8'
    implementation "org.json:json:$jsonVersion"
    implementation group: 'com.google.code.gson', name: 'gson', version: "$gsonVersion"
    implementation group: 'com.google.guava', name: 'guava', version: "$guavaVersion"
    implementation project(':RMBTSharedCode')
    implementation project(':RMBTUtil')

    testImplementation group: 'org.jmockit', name: 'jmockit', version: '1.45'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

sourceSets {
    main {
        resources { srcDir 'src' }
    }
}

subprojects{
    evaluationDependsOn(':RMBTSharedCode')
    evaluationDependsOn(':RMBTUtil')
}

jar {
    manifest {
        attributes 'Main-Class': 'at.rtr.rmbt.client.RMBTClientRunner'
    }
}

shadowJar {
    baseName = 'RMBTClient'
}

def revisionPropertiesFile = file("${buildDir}/resources/main/revision.properties")
processResources {
    doLast {
        String revisionProperties = "git.describe=" + getVersionNameFromGit() + "\n"
        revisionProperties = revisionProperties + "git.branch=" + getBranchFromGit() + "\n"
        revisionProperties = revisionProperties + "git.dirty=" + getDirtyFromGit() + "\n"
        revisionProperties = revisionProperties + "git.id=" + getHashFromGit() + "\n"
        revisionProperties = revisionProperties + "git.revision=" + getVersionCodeFromGit() + "\n"
        revisionPropertiesFile.write(revisionProperties, 'UTF-8')
    }
}

test {
    jvmArgs "-javaagent:${classpath.find { it.name.contains("jmockit") }.absolutePath}"
}